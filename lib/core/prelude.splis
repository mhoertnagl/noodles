(do
  ;; TODO: (println ...)

  ;; `fun` provides a macro to streamline function definition.
  ;; Instead of defining the not-function the conventional way
  ;;
  ;; ```(def not (fn [x] (if x false true)))```
  ;;
  ;; one can leverage `defn` to achief a mor concise definition:
  ;;
  ;; ```(defn not [x] (if x false true))```
  ;;
  ;; @sym   name  The name of the function.
  ;; @(sym) args  The list of arument symbols.
  ;; @(any) body  The body of the function.
  (defmacro defn
    (fn [name args body]
      `(def ~name (fn ~args ~body)) ))

  ;; `use` reads, parses and evaluates a splis module.
  ;;
  ;; To use the module in the file `<SPLIS_HOME>/lib/core/math.splis` add the
  ;; following command before the usage of any method in `math.splis`:
  ;;
  ;; ```(use "lib/core/math")```
  ;;
  ;; All imported modules need to be located in the derectory the environment
  ;; variable <SPLIS_HOME> points to and the file extension `.splis` must be
  ;; omitted.
  ;;
  ;; @str module  The module.
  (defn use [module]
    (eval (parse (read-file (join *SPLIS_HOME* module ".splis")))))

  ;; `nil?` returns true if and only if `x` is `nil`.
  ;;
  ;; @any x  A value.
  ;; @return [bool] `true` iff `x` is `nil`.
  (defn nil? [x] (= x nil))

  ;; `true?` returns true if and only if `x` is the boolean `true`.
  ;;
  ;; @any x  A value.
  ;; @return [bool] `true` iff `x` is the boolean `true`.
  (defn true? [x] (= x true))

  ;; `false?` returns true if and only if `x` is the boolean `false`.
  ;;
  ;; @any x  A value.
  ;; @return [bool] `true` iff `x` is the boolean `false`.
  (defn false? [x] (= x false))

  ;; `not` returns `false` if `x` is truthy; `true` otherwise. `not` is an
  ;; alias for `false?`.
  ;;
  ;; @any x  A value.
  ;; @return [bool] `false` if `x` is truthy; `true` otherwise.
  (def not false?)

  ;; `else` is an alias for the boolean atom `true`. Used in `cond` expressions
  ;; to denote the default branch.
  (def else true)

  ;; `identity` returns the argument unchanged.
  ;;
  ;; @any x  Any argument.
  ;; @return [any] `x` unchanged.
  (defn identity [x] x)
  ; TODO: This does not work.
  ; (defmacro identity (fn [x] x))

  ;; `range` returns the list of numbers from `start` (inclusive) to `end`
  ;; (exclusive). If `start >= end` it will return the empty list.
  ;; `step` denotes the increment added to `start` until it is greater or equal
  ;; to `end`.
  ;;
  ;; @num start  The start number (included).
  ;; @num end    The end number (excluded).
  ;; @num step   The increment step size.
  ;; @return [(num)] The sequence of numbers from `a` up to `b`.
  (defn range [start end step]
    (if (>= start end)
      ()
      (:: start (range (+ start step) end step))))

  ;; `irange` returns the list of integers from `start` (inclusive) to `end`
  ;; (exclusive). If `start >= end` it will return the empty list.
  ;; This is the same as `(range start end 1)`.
  ;;
  ;; @num start  The start number (included).
  ;; @num end    The end number (excluded).
  ;; @return [(num)] The sequence of integers from `a` up to `b`.
  (defn irange [start end] (range start end 1))

  ;; TODO: clojure uses mapv for vectors.

  ;; `map` applies a function of one argument to each element of a list and
  ;; returns a list of the application results in the same order as the
  ;; elements.
  ;;
  ;; @fun(T)any f   A unary function.
  ;; @(T)       xs  A list of elements.
  ;; @return  The list of elements applied to the unary function individually.
  (defn map [f xs]
    (if (empty? xs)
      xs ; Will preserve the type of the sequence.
      (:: (f (head xs)) (map f (tail xs)))))

  ; (defn flat-map [p xs]
  ;   (if (empty? xs)
  ;     ()
  ;     (::: (f (head xs)) (map f (tail xs)))))

  ;; `reduce` reduces a sequence of elements into a single element by successive
  ;; application of the binary function `f` in a left-associative fashion where
  ;; `b` is the initial value.
  ;; If the list is empty, `reduce` will return `b`.
  ;;
  ;; @fun(T S)T f   A binary function.
  ;; @T         b   The initial value.
  ;; @(S)       xs  A list of elements.
  ;; @return  The left-associative reduction of `xs` into a single element.
  (defn reduce [f b xs]
    (if (empty? xs)
      b
      (reduce f (f b (head xs)) (tail xs))))

  ; (defn reduce-right [f b xs]
  ;   (if (empty? xs)
  ;     b
  ;     (f b (reduce f (head xs) (tail xs)))))

  (defn filter [p xs]
    (cond
      (empty? xs) xs
      (p (head xs)) (:: (head xs) (filter p (tail xs)))
      else (filter p (tail xs)) ))

  (defn remove [p xs] (filter (fn [x] (not (p x))) xs))

  ;; (defn all [p xs])
  ;; (defn any [p xs])
  ;; (defn reverse [xs])

  ; (defn drop [n xs]
  ;   (cond
  ;     (empty? xs) xs
  ;     (<= n 0) xs
  ;     else (drop (- n 1) (tail xs)) ))

  ; (defn take [n xs]
  ;   (cond
  ;     (empty? xs) xs
  ;     (<= n 0) ()
  ;     else (:: (head xs) (take (- n 1) (tail xs))) ))

  ; (defn drop-while [p xs]
  ;   (cond
  ;     (empty? xs) xs
  ;     (p (head xs)) (drop-while p (tail xs))
  ;     else xs ))

  ; (defn take-while [p xs]
  ;   (cond
  ;     (empty? xs) xs
  ;     (p (head xs)) (:: (head xs) (take-while p (tail xs)))
  ;     else () ))

  ;; (defn zip-with [f xs ys])
  ;; (defn zip [xs ys])

  ; (defn repeat [n v]
  ;   (if (<= n 0)
  ;     ()
  ;     (:: v (repeat (- n 1) v)) ))

  ;; (defn flatten [xs])
  (use "lib/core/math")
  (use "lib/core/io")
)
